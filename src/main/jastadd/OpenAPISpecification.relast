//OpenAPI Object
OpenAPIObject ::= <OpenAPI> <JsonSchemaDialect> Info:InfoObject Servers:ServerObject* Paths:PathsObject* Webhooks:Webhook* Components:ComponentsObject SecurityRequirements:SecurityRequirementObject* Tags:TagObject* [ExternalDocs:ExternalDocObject] Extensions:Extension* ;
Webhook ::= <Key> PathItem:PathItemType;

//Info Object
InfoObject ::= <Title> <Summary> <Description> <TermsOfService> <Version> [Contact:ContactObject] [License:LicenseObject] Extensions:Extension*;

//Contact Object
ContactObject ::= <Name> <Url> <Email> Extensions:Extension*;

//License Object
LicenseObject ::= <Name> <Identifier> <Url> Extensions:Extension*;

//Server Object
ServerObject ::= <Url> <Description> ServerVariables:ServerVariablesTuple* Extensions:Extension*;
ServerVariablesTuple ::= <Name> Variable:ServerVariableObject;

//Server Variable Object
ServerVariableObject ::= <Default> <Description> Enums:Enum* Extensions:Extension*;
Enum ::= <EnumValue>;

//Components Object
ComponentsObject ::= Schemas:SchemaTuple* Responses:ResponseTuple* Parameters:ParameterTuple* Examples:ExampleTuple* RequestBodies:RequestBodyTuple* Headers:HeaderTuple* SecuritySchemes:SecuritySchemeTuple* Links:LinkTuple* Callbacks:CallbackTuple* PathItems:PathItemTuple* Extensions:Extension*;

SchemaTuple ::= <Key> Schema:SchemaType;
ResponseTuple ::= <Key> Response:ResponseType;
ParameterTuple ::= <Key> Parameter:ParameterType;
ExampleTuple ::= <Keyv> Example:ExampleType;
RequestBodyTuple ::= <Key> RequestBody:RequestBodyType;
HeaderTuple ::= <Key> Header:HeaderType;
SecuritySchemeTuple ::= <Key> SecurityScheme:SecuritySchemeType;
LinkTuple ::= <Key> Link:LinkType;
CallbackTuple ::= <Key> Callback:CallbackType;
PathItemTuple ::= <Key> PathItem:PathItemType;

//Paths Object
PathsObject ::= <Ref> PathItem:PathItemType Extensions:Extension*;

//Path Item Object
abstract PathItemType;
PathItemReference : PathItemType ::= <Ref> <Summary> <Description>;
rel PathItemReference.reference -> PathItemObject;
PathItemObject : PathItemType ::= <Ref> <Summary> <Description> [GetOperation:Get] [PutOperation:Put] [PostOperation:Post] [DeleteOperation:Delete] [OptionsOperation:Options] [HeadOperation:Head] [PatchOperation:Patch] [TraceOperation:Trace] Servers:ServerObject* Parameters:ParameterType* Extensions:Extension*;

//Operation Object
OperationObject ::= <Summary> <Description> <OperationID> <Deprecated:Boolean> Tags:Tag* [ExternalDocs:ExternalDocObject] Parameters:ParameterType* [RequestBody:RequestBodyType] Responses:ResponsesObject Callbacks:CallbackTuple* SecurityRequirements:SecurityRequirementObject* Servers:ServerObject* Extensions:Extension*;

Get ::= Operation:OperationObject;
Put ::= Operation:OperationObject;
Post ::= Operation:OperationObject;
Delete ::= Operation:OperationObject;
Options ::= Operation:OperationObject;
Head ::= Operation:OperationObject;
Patch ::= Operation:OperationObject;
Trace ::= Operation:OperationObject;
Tag ::= <Tag>;

//External Documentation Object
ExternalDocObject ::= <Description> <Url> Extensions:Extension*;

//Parameter Object
abstract ParameterType;
ParameterReference : ParameterType ::= <Ref> <Summary> <Description>;
rel ParameterReference.reference -> ParameterObject;
ParameterObject : ParameterType ::= <Name> <In> <Description> <Required:Boolean> <Deprecated:Boolean> <AllowEmptyValue:Boolean> <Style> <Explode:Boolean> <AllowReserved:Boolean> <Example:Object> [Schema:SchemaType] Examples:ExampleTuple* Content:ContentTuple* Extensions:Extension*;
ContentTuple ::= <Key> MediaType:MediaTypeObject;

//Request Body Object
abstract RequestBodyType;
RequestBodyReference : RequestBodyType ::= <Ref> <Summary> <Description>;
rel RequestBodyReference.reference -> RequestBodyObject;
RequestBodyObject : RequestBodyType ::= <Description> <Required:Boolean> Content:ContentTuple* Extensions:Extension*;

//Media Type Object
MediaTypeObject ::= <Example:Object> [Schema:SchemaType] Examples:ExampleTuple* Encodings:EncodingTuple* Extensions:Extension*;
EncodingTuple ::= <Key> Encoding:EncodingObject;

//Encoding Object
EncodingObject ::= <ContentType> <Style> <Explode:Boolean> <AllowReserved:Boolean> Headers:HeaderTuple* Extensions:Extension*;

//Responses Object
ResponsesObject ::= Responses:ResponseTuple*;

//Response Object
abstract ResponseType;
ResponseReference : ResponseType ::= <Ref> <Summary> <Description>;
rel ResponseReference.reference -> ResponseObject;
ResponseObject : ResponseType ::= <Description> Headers:HeaderTuple* Contents:ContentTuple* Links:LinkTuple* Extensions:Extension*;

//Callback Object
abstract CallbackType;
CallbackReference : CallbackType ::= <Ref> <Summary> <Description>;
rel CallbackReference.reference -> CallbackObject;
CallbackObject : CallbackType ::= Expressions:Expression* Extensions:Extension*;

Extension ::= <Key> <Value:Object>;
Expression ::= <Name> PathItem:PathItemType;

//Example Object
abstract ExampleType;
ExampleReference : ExampleType ::= <Ref> <Summary> <Description>;
rel ExampleReference.reference -> ExampleObject;
ExampleObject : ExampleType ::= <Summary> <Description> <Value:Object> <ExternalValue> Extensions:Extension*;

//Link Object
abstract LinkType;
LinkReference : LinkType ::= <Ref> <Summary> <Description>;
rel LinkReference.reference -> LinkObject;
LinkObject : LinkType ::= <OperationRef> <OperationID> <LinkRequestBody:Object> <Description> LinkParameters:LinkParameterTuple* [Server:ServerObject] Extensions:Extension*;

LinkParameterTuple ::= <Key> <Value>;

//Header Object
abstract HeaderType;
HeaderReference : HeaderType ::= <Ref> <Summary> <Description>;
rel HeaderReference.reference -> HeaderObject;
HeaderObject : HeaderType ::= <Description> <Required:Boolean> <DeprecatedBoolean:Boolean> <AllowEmptyValue:Boolean> <Style> <Explode:Boolean> <AllowReserved:Boolean> <Example:Object> [Schema:SchemaType] Examples:ExampleTuple* Contents:ContentTuple* Extensions:Extension*;

//Tag Object
TagObject ::= <Name> <Description> [ExternalDoc:ExternalDocObject] Extensions:Extension*;

//Schema Object
abstract SchemaType;
SchemaReference : SchemaType ::= <Ref> <Summary> <Description>;
rel SchemaReference.reference -> SchemaObject;
SchemaObject : SchemaType ::= <Name> <AdditionalProperties:Object> <DefaultValue:Object> <Description> <DeprecatedBoolean:Boolean> <ExclusiveMaximum:Boolean> <ExclusiveMinimum:Boolean> <Format> <Maximum:BigDecimal> <Minimum:BigDecimal> <MaxItems:Integer> <MinItems:Integer> <MaxLength:Integer> <MinLength:Integer> <MaxProperties:Integer> <MinProperties:Integer> <MultipleOf:BigDecimal> <Pattern> <ReadOnly:Boolean> <WriteOnly:Boolean> <Type> <Title> <UniqueItems:Boolean> <Nullable:Boolean> <MaxContains:Integer> <MinContains:Integer> <DependentRequired:Object> <DependentSchema:SchemaType> <Const:Object> [Discriminator:DiscriminatorObject] Enum:EnumObject* ExampleElements:ExampleElement* [ExternalDoc:ExternalDocObject] [ItemsSchema:ItemsSchemaObject] [NotSchema:NotSchemaObject] PropertyItems:PropertyItem* RequiredFields:RequiredField* AllOfSchema:AllOfSchemaObject* AnyOfSchema:AnyOfSchemaObject* OneOfSchema:OneOfSchemaObject* Types:TypeArray* [Xml:XmlObject] Extensions:Extension*;

ItemsSchemaObject ::= Schema:SchemaType;
NotSchemaObject ::= Schema:SchemaType;
PropertyItem ::= <Name> Schema:SchemaType;
AllOfSchemaObject ::= Schema:SchemaType;
AnyOfSchemaObject ::= Schema:SchemaType;
OneOfSchemaObject ::= Schema:SchemaType;
ExampleElement ::= <Example:Object>;
TypeArray ::= <TypeElements:Object>;
EnumObject ::= <Enum:Object>;
RequiredField ::= <Value>;

//Discriminator Object
DiscriminatorObject ::= <PropertyName> Mappings:MappingTuple* Extensions:Extension*;
MappingTuple ::= <Key> <Value>;

//XML Object
XmlObject ::= <Name> <Namespace> <Prefix> <Attribute:Boolean> <Wrapped:Boolean> Extensions:Extension*;

//Security Scheme Object
abstract SecuritySchemeType;
SecuritySchemeReference : SecuritySchemeType ::= <Ref> <Summary> <Description>;
rel SecuritySchemeReference.reference -> SecuritySchemeObject;
SecuritySchemeObject : SecuritySchemeType ::= <Type> <Description> <Name> <In> <Scheme> <BearerFormat> <OpenIdConnectUrl> [OAuthFlows:OAuthFlowsObject] Extensions:Extension*;

//OAuth Flows Object
OAuthFlowsObject ::= [Implicit:OAuthFlowObject] [Password:OAuthFlowObject] [ClientCredentials:OAuthFlowObject] [AuthorizationCode:OAuthFlowObject] Extensions:Extension*;

//OAuth Flow Object
OAuthFlowObject ::= <AuthorizationUrl> <TokenUrl> <RefreshUrl> Scopes:ScopesTuple* Extensions:Extension*;
ScopesTuple ::= <ScopesKey> <ScopesValue>;

//Security Requirement Object
SecurityRequirementObject ::= Tuple:SecurityRequirementTuple*;

SecurityRequirementTuple ::= <Name> Value:SecurityRequirementValue*;
SecurityRequirementValue ::= <Value>;